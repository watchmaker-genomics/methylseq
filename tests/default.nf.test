nextflow_pipeline {

    name "Test pipeline"
    script "../main.nf"
    config "./nextflow.config"
    tag "pipeline"
    tag "cpu"

    test("-profile test") {
        when {
            params {
                outdir = "${outputDir}"
            }
        }

        then {
            // stable_name: All files + folders in ${params.outdir}/ with a stable name
            def stable_name = getAllFilesFromDir(params.outdir, relative: true, includeDir: true, ignore: ['pipeline_info/*.{html,json,txt}'])
            // stable_path: All files in ${params.outdir}/ with stable content
            def stable_path = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore')
            // bam_files: All bam files
            def bam_files  = getAllFilesFromDir(params.outdir, include: ['**/*.bam'])
            assertAll(
                { assert workflow.success},
                { assert snapshot(
                    // Number of tasks
                    workflow.trace.succeeded().size(),
                    // pipeline versions.yml file for multiqc from which Nextflow version is removed because we tests pipelines on multiple Nextflow versions
                    removeNextflowVersion("${outputDir}/pipeline_info/nf_core_methylseq_software_mqc_versions.yml"),
                    // All stable path name
                    stable_name,
                    // All files with stable contents
                    stable_path,
                    // All bam files
                    bam_files.collect{ file -> [ file.getName(), bam(file.toString()).getReadsMD5() ] }
                ).match() }
            )
        }
    }

    test("Params: default with bowtie2-index") {
        when {
            params {
                bismark_index = "https://github.com/nf-core/test-datasets/raw/methylseq/reference/Bowtie2_Index.tar.gz"
                outdir        = "${outputDir}"
            }
        }

        then {
            assertAll(
                { assert workflow.success },
                { assert snapshot(
                    // Number of tasks
                    workflow.trace.succeeded().size()
                ).match() }
            )
        }
    }

}
