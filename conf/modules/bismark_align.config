process {
    withName: BISMARK_ALIGN {
            ext.args = { [
                (params.aligner == 'bismark_hisat') ? ' --hisat2' : ' --bowtie2',
                (params.aligner == 'bismark_hisat' && params.known_splices) ? " --known-splicesite-infile <(hisat2_extract_splice_sites.py ${params.known_splices})" : '',
                params.pbat ? ' --pbat' : '',
                ( params.single_cell || params.non_directional || params.zymo ) ? ' --non_directional' : '',
                params.unmapped ? ' --unmapped' : '',
                params.relax_mismatches ? " --score_min L,0,-${params.num_mismatches}" : '',
                params.local_alignment ? " --local" : '',
                !meta.single_end && params.minins ? " --minins ${params.minins}" : '',
                meta.single_end ? '' : (
                    params.maxins ? " --maxins ${params.maxins}" : (
                        params.em_seq ? " --maxins 1000" : ''
                    )
                )
            ].join(' ').trim() }
            publishDir = [
                [
                    path: { "${params.outdir}/${params.aligner}/alignments/unmapped" },
                    mode: params.publish_dir_mode,
                    pattern: "*.fq.gz",
                    enabled: params.unmapped
                ],
                [
                    path: { "${params.outdir}/${params.aligner}/alignments" },
                    mode: params.publish_dir_mode,
                    pattern: "*.bam",
                    enabled: (params.save_align_intermeds || params.skip_deduplication || params.rrbs)
                ],
                [
                    path: { "${params.outdir}/${params.aligner}/alignments/logs" },
                    mode: params.publish_dir_mode,
                    pattern: "*.txt"
                ]
            ]
        }
}
